' Furry Art Studio - 本地稿件管理工具
' Copyright 2026 xionglongztz
'
' Licensed under the Apache License, Version 2.0 (the "License");
' you may not use this file except in compliance with the License.
' You may obtain a copy of the License at
'
'     http://www.apache.org/licenses/LICENSE-2.0
'
' Unless required by applicable law or agreed to in writing, software
' distributed under the License is distributed on an "AS IS" BASIS,
' WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
' See the License for the specific language governing permissions and
' limitations under the License.
Imports System.IO
Imports System.Runtime.InteropServices
Imports System.Text.RegularExpressions
Imports System.Threading

Public Class ViewForm
    Implements IThemeChangeable

#Region "私有字段"
    '稿件
    Private _currentArtwork As Artwork '当前稿件
    Private _allArtworks As List(Of Artwork) '全部稿件列表
    '索引
    Private _currentFileIndex As Integer = 0 '当前文件索引
    Private _currentArtworkIndex As Integer = -1 '当前稿件索引
    '异步
    Private _isProcessing As Boolean = False '正在处理信号量
    Private _loadingLock As New Object() '锁对象
    Private _loadingTask As Task '异步加载任务
    Private _cancellationTokenSource As CancellationTokenSource '任务取消令牌
    '事件
    Private _mainForm As Form '保存主窗口引用
    '扩展名
    Private ReadOnly _imageExtensions As String() = {".jpg", ".jpeg", ".png", ".gif", ".bmp", ".tiff", ".ico", ".webp"}
    '菜单
    Private Const SC_PREVIMG = 1
    Private Const SC_NEXTIMG = 2
    Private Const SC_PREVART = 3
    Private Const SC_NEXTART = 4
    Private Const SC_ALWAYSONTOP = 5
    Private Const SC_COPY = 6
    Private Const SC_INFO = 7
    Private Const SC_PLAY = 8
    Private Const SC_HELP = 9
    Private Const SC_FULLSCREEN = 10
#End Region

#Region "窗体相关"
    ''' <summary>
    ''' 构造函数 - 接收当前稿件和所有稿件列表
    ''' </summary>
    Public Sub New(currentArtwork As Artwork, allArtworks As List(Of Artwork))
        InitializeComponent()
        _currentArtwork = currentArtwork
        _allArtworks = allArtworks
        _mainForm = MainForm
        '查找当前稿件在所有稿件列表中的索引
        If _allArtworks IsNot Nothing Then
            _currentArtworkIndex = _allArtworks.FindIndex(Function(a) a.ID = currentArtwork.ID)
        End If
        If TypeOf _mainForm Is MainForm Then
            AddHandler DirectCast(_mainForm, MainForm).LibraryClosed, AddressOf OnLibraryClosed
        End If
    End Sub

    ''' <summary>
    ''' 窗体加载事件
    ''' </summary>
    Private Sub ViewForm_Load(sender As Object, e As EventArgs) Handles MyBase.Load
        Me.KeyPreview = True
        Me.Text = "图片浏览器"
        PictureBoxMain.SizeMode = PictureBoxSizeMode.Zoom
        PictureBoxMain.Dock = DockStyle.Fill
        SysMenuInit() '初始化菜单
        UpdateMenuStates() '更新菜单状态
        SystemThemeChange() '初始化主题
        '加载当前稿件的第一张图片
        LoadCurrentArtworkFirstImage()
    End Sub

    ''' <summary>
    ''' 窗体关闭时释放资源
    ''' </summary>
    Private Sub ViewForm_FormClosing(sender As Object, e As FormClosingEventArgs) Handles MyBase.FormClosing
        If _mainForm IsNot Nothing AndAlso TypeOf _mainForm Is MainForm Then
            RemoveHandler DirectCast(_mainForm, MainForm).LibraryClosed, AddressOf OnLibraryClosed
        End If
        If PictureBoxMain.Image IsNot Nothing Then
            PictureBoxMain.Image.Dispose()
            PictureBoxMain.Image = Nothing
        End If
    End Sub
    Public Sub SystemThemeChange() Implements IThemeChangeable.SystemThemeChange
        If IsDarkMode() Then
            PictureBoxMain.BackColor = BgColorDark
            Icon = CreateRoundedRectangleIcon(True, My.Resources.Icons.FormImageDark)
            InitializeMenuImages(True) '设置菜单图标主题
        Else
            PictureBoxMain.BackColor = BgColorLight
            Icon = CreateRoundedRectangleIcon(False, My.Resources.Icons.FormImageLight)
            InitializeMenuImages()
        End If
        'WinAPI
        DwmSetWindowAttribute(Handle, DwmWindowAttribute.UseImmersiveDarkMode, IsDarkMode(), Marshal.SizeOf(Of Integer))
        SetPreferredAppMode(If(IsDarkMode(), PreferredAppMode.AllowDark, PreferredAppMode.ForceLight))
        FlushMenuThemes()
    End Sub
    ''' <summary>
    ''' 初始化系统菜单
    ''' </summary>
    Private Sub SysMenuInit()
        Dim menuHandle = GetSystemMenu(Handle, False) '获取菜单句柄
        InsertMenu(menuHandle, 0, MF_BYPOSITION Or MF_STRING, SC_PREVIMG, "上一张(&P)")
        InsertMenu(menuHandle, 1, MF_BYPOSITION Or MF_STRING, SC_NEXTIMG, "下一张(&N)")
        InsertMenu(menuHandle, 2, MF_BYPOSITION Or MF_SEPARATOR, 0, Nothing)
        InsertMenu(menuHandle, 3, MF_BYPOSITION Or MF_STRING, SC_PREVART, "上一稿件(&R)")
        InsertMenu(menuHandle, 4, MF_BYPOSITION Or MF_STRING, SC_NEXTART, "下一稿件(&E)")
        InsertMenu(menuHandle, 5, MF_BYPOSITION Or MF_SEPARATOR, 0, Nothing)
        InsertMenu(menuHandle, 6, MF_BYPOSITION Or MF_STRING, SC_ALWAYSONTOP, "窗口置顶(&T)")
        InsertMenu(menuHandle, 7, MF_BYPOSITION Or MF_STRING, SC_COPY, "复制(&C)")
        InsertMenu(menuHandle, 8, MF_BYPOSITION Or MF_SEPARATOR, 0, Nothing)
        InsertMenu(menuHandle, 9, MF_BYPOSITION Or MF_STRING, SC_INFO, "详情(&I)...")
        InsertMenu(menuHandle, 10, MF_BYPOSITION Or MF_STRING, SC_PLAY, "幻灯片放映(&P)")
        InsertMenu(menuHandle, 11, MF_BYPOSITION Or MF_STRING, SC_HELP, "帮助(&H)...")
        InsertMenu(menuHandle, 12, MF_BYPOSITION Or MF_SEPARATOR, 0, Nothing)
        InsertMenu(menuHandle, 13, MF_BYPOSITION Or MF_STRING, SC_FULLSCREEN, "全屏(&F)")
        '设置菜单项快捷键
        SetMenuItemWithShortcut(menuHandle, 0, SC_PREVIMG, "上一张(&P)", "PageUp")
        SetMenuItemWithShortcut(menuHandle, 1, SC_NEXTIMG, "下一张(&N)", "PageDown")
        SetMenuItemWithShortcut(menuHandle, 3, SC_PREVART, "上一稿件(&R)", "Ctrl+Left")
        SetMenuItemWithShortcut(menuHandle, 4, SC_NEXTART, "下一稿件(&E)", "Ctrl+Right")
        SetMenuItemWithShortcut(menuHandle, 6, SC_ALWAYSONTOP, "窗口置顶(&T)", "Alt+T")
        SetMenuItemWithShortcut(menuHandle, 7, SC_COPY, "复制(&C)", "Ctrl+C")
        SetMenuItemWithShortcut(menuHandle, 9, SC_INFO, "详情(&I)...", "I")
        SetMenuItemWithShortcut(menuHandle, 10, SC_PLAY, "幻灯片放映(&P)", "Ctrl+F5")
        SetMenuItemWithShortcut(menuHandle, 11, SC_HELP, "帮助(&H)...", "F1")
        SetMenuItemWithShortcut(menuHandle, 13, SC_FULLSCREEN, "全屏(&F)", "F11")
    End Sub
    Private Sub InitializeMenuImages(Optional isDarkMode As Boolean = False)
        Dim menuHandle = GetSystemMenu(Handle, False) '设置窗体菜单
        If isDarkMode Then
            ApplyMenuIcon(menuHandle, SC_PREVIMG, My.Resources.Icons.MenuPreviousDark, True)
            ApplyMenuIcon(menuHandle, SC_NEXTIMG, My.Resources.Icons.MenuNextDark, True)
            ApplyMenuIcon(menuHandle, SC_PREVART, My.Resources.Icons.MenuLeftDark, True)
            ApplyMenuIcon(menuHandle, SC_NEXTART, My.Resources.Icons.MenuRightDark, True)
            ApplyMenuIcon(menuHandle, SC_ALWAYSONTOP, My.Resources.Icons.MenuPinDark, True)
            ApplyMenuIcon(menuHandle, SC_COPY, My.Resources.Icons.MenuCopyDark, True)
            ApplyMenuIcon(menuHandle, SC_INFO, My.Resources.Icons.MenuInfoDark, True)
            ApplyMenuIcon(menuHandle, SC_PLAY, My.Resources.Icons.MenuImagePlayDark, True)
            ApplyMenuIcon(menuHandle, SC_HELP, My.Resources.Icons.MenuTutorialDark, True)
            ApplyMenuIcon(menuHandle, SC_FULLSCREEN, My.Resources.Icons.MenuFullscreenDark, True)
        Else
            ApplyMenuIcon(menuHandle, SC_PREVIMG, My.Resources.Icons.MenuPreviousLight)
            ApplyMenuIcon(menuHandle, SC_NEXTIMG, My.Resources.Icons.MenuNextLight)
            ApplyMenuIcon(menuHandle, SC_PREVART, My.Resources.Icons.MenuLeftLight)
            ApplyMenuIcon(menuHandle, SC_NEXTART, My.Resources.Icons.MenuRightLight)
            ApplyMenuIcon(menuHandle, SC_ALWAYSONTOP, My.Resources.Icons.MenuPinLight)
            ApplyMenuIcon(menuHandle, SC_COPY, My.Resources.Icons.MenuCopyLight)
            ApplyMenuIcon(menuHandle, SC_INFO, My.Resources.Icons.MenuInfoLight)
            ApplyMenuIcon(menuHandle, SC_PLAY, My.Resources.Icons.MenuImagePlayLight)
            ApplyMenuIcon(menuHandle, SC_HELP, My.Resources.Icons.MenuTutorialLight)
            ApplyMenuIcon(menuHandle, SC_FULLSCREEN, My.Resources.Icons.MenuFullscreenLight)
        End If
    End Sub
    Protected Overrides Sub WndProc(ByRef m As Message) '窗体消息处理函数
        If m.Msg = WM_SYSCOMMAND Then '窗体响应菜单
            Dim hMenu = GetSystemMenu(Handle, False)
            Select Case m.WParam.ToInt32'对应菜单标号
                Case SC_PREVIMG '上一张
                    NavigatePrevious()
                Case SC_NEXTIMG '下一张
                    NavigateNext()
                Case SC_PREVART '上个稿件
                    NavigatePreviousArtwork()
                Case SC_NEXTART '下个稿件
                    NavigateNextArtwork()
                Case SC_ALWAYSONTOP '窗口置顶
                    SetWindowOnTop()
                Case SC_COPY '复制
                    Clipboard.SetImage(PictureBoxMain.Image)
                Case SC_INFO '详情
                    ShowArtworkInfo()
                Case SC_PLAY'幻灯片放映
                    '待开发
                Case SC_HELP '帮助
                    ShowHelp()
                Case SC_FULLSCREEN '全屏
                    '待开发
            End Select
        End If
        MyBase.WndProc(m) '循环监听消息
    End Sub
#End Region

#Region "辅助函数"
    ''' <summary>
    ''' 从文件路径数组中过滤出图片文件
    ''' </summary>
    ''' <param name="filePaths">文件夹</param>
    ''' <returns>图片文件路径</returns>
    Private Function GetImageFiles(filePaths As String()) As List(Of String)
        Dim result As New List(Of String)
        If filePaths Is Nothing Then Return result '没有文件
        For Each p In filePaths
            '跳过预览图文件
            If Path.GetFileName(p).ToLower() = ".preview.jpg" Then
                Continue For
            End If
            Dim ext As String = Path.GetExtension(p).ToLower()
            If _imageExtensions.Contains(ext) Then
                result.Add(p)
            End If
        Next
        '按文件名排序
        'result = result.OrderBy(Function(p) p, New NaturalStringComparer()).ToList()
        result.Sort(Function(a, b) StrCmpLogicalW(Path.GetFileName(a), Path.GetFileName(b)))
        Return result
    End Function
#Region "自然字符串排序比较器"
    Public Class NaturalStringComparer
        Implements IComparer(Of String)
        Private Shared ReadOnly _regex As New Regex("\d+", RegexOptions.Compiled)
        Public Function Compare(x As String, y As String) As Integer Implements IComparer(Of String).Compare
            Return CompareNatural(x, y)
        End Function
        Private Shared Function CompareNatural(x As String, y As String) As Integer
            '提取文件名
            Dim xFile = Path.GetFileNameWithoutExtension(x)
            Dim yFile = Path.GetFileNameWithoutExtension(y)
            Dim xMatches = _regex.Matches(xFile)
            Dim yMatches = _regex.Matches(yFile)
            '如果没有数字, 使用普通字符串比较
            If xMatches.Count = 0 OrElse yMatches.Count = 0 Then
                Return String.Compare(xFile, yFile, StringComparison.OrdinalIgnoreCase)
            End If
            '逐个比较数字部分
            Dim i As Integer = 0
            While i < Math.Min(xMatches.Count, yMatches.Count)
                Dim xNum As Integer
                Dim yNum As Integer
                Dim xOk = Integer.TryParse(xMatches(i).Value, xNum)
                Dim yOk = Integer.TryParse(yMatches(i).Value, yNum)

                '如果解析失败, 回退到字符串比较
                If Not xOk OrElse Not yOk Then
                    Return String.Compare(xFile, yFile, StringComparison.OrdinalIgnoreCase)
                End If

                If xNum <> yNum Then
                    Return xNum.CompareTo(yNum)
                End If
                i += 1
            End While
            '如果数字部分都相同, 比较数字段的数量
            Return xMatches.Count.CompareTo(yMatches.Count)
        End Function
    End Class
#End Region

    ''' <summary>
    ''' 加载当前稿件的第一个有效图片
    ''' </summary>
    Private Sub LoadCurrentArtworkFirstImage()
        If _currentArtwork Is Nothing OrElse _currentArtwork.FilePaths Is Nothing Then
            MessageBox.Show("当前稿件目录为空", "Furry Art Studio", MessageBoxButtons.OK, MessageBoxIcon.Information)
            Me.Close()
            Return
        End If
        Dim imageFiles As List(Of String) = GetImageFiles(_currentArtwork.FilePaths)
        If imageFiles.Count = 0 Then
            MessageBox.Show("当前稿件没有支持的图片格式文件", "Furry Art Studio", MessageBoxButtons.OK, MessageBoxIcon.Information)
            Me.Close()
            Return
        End If
        _currentFileIndex = 0
        LoadImageAsync(imageFiles(_currentFileIndex))
    End Sub

    ''' <summary>
    ''' 获取当前稿件的所有图片文件
    ''' </summary>
    ''' <returns>图片文件路径</returns>
    Private Function GetCurrentArtworkImages() As List(Of String)
        If _currentArtwork Is Nothing OrElse _currentArtwork.FilePaths Is Nothing Then
            Return New List(Of String)
        End If
        Return GetImageFiles(_currentArtwork.FilePaths)
    End Function

    ''' <summary>
    ''' 更新窗口标题
    ''' </summary>
    ''' <param name="currentFilePath">当前文件路径</param>
    Private Sub UpdateWindowTitle(Optional currentFilePath As String = Nothing)
        If _currentArtwork Is Nothing Then '没有文件
            Me.Text = "图片浏览器"
            Return
        End If
        Dim title As String = _currentArtwork.Title
        If String.IsNullOrWhiteSpace(title) Then '没有标题
            title = "无标题"
        End If
        Dim imageFiles As List(Of String) = GetCurrentArtworkImages()
        Dim totalImages As Integer = imageFiles.Count
        If totalImages = 0 Then
            Me.Text = $"{title} - 图片浏览器 [0/0]"
        Else
            Dim fileName As String = ""
            If currentFilePath IsNot Nothing Then
                fileName = Path.GetFileName(currentFilePath)
            End If
            '显示格式: 标题 - [当前文件索引/总文件数] 文件名 - 图片浏览器 (当前稿件索引/总稿件数)
            If _currentArtworkIndex >= 0 AndAlso _allArtworks IsNot Nothing Then
                Me.Text = $"{title} - [{_currentFileIndex + 1}/{totalImages}] {fileName} - 图片浏览器 ({_currentArtworkIndex + 1}/{_allArtworks.Count})"
            Else
                Me.Text = $"{title} - [{_currentFileIndex + 1}/{totalImages}] {fileName} - 图片浏览器"
            End If
            UpdateMenuStates() '同时更新标题
        End If
    End Sub

    ''' <summary>
    ''' 显示稿件信息
    ''' </summary>
    Private Sub ShowArtworkInfo()
        If _currentArtwork Is Nothing Then Return
        Dim info As String = $"标题: {_currentArtwork.Title}{vbCrLf}" &
                             $"作者: {_currentArtwork.Author}{vbCrLf}" &
                             $"UUID: {_currentArtwork.UUID}{vbCrLf}" &
                             $"角色: {FormatArrayWithEllipsis(_currentArtwork.Characters)}{vbCrLf}" &
                             $"标签: {FormatArrayWithEllipsis(_currentArtwork.Tags)}{vbCrLf}" &
                             $"创作时间: {_currentArtwork.CreateTime:yyyy-MM-dd HH:mm:ss}{vbCrLf}" &
                             $"入库时间: {_currentArtwork.ImportTime:yyyy-MM-dd HH:mm:ss}{vbCrLf}" &
                             $"更新时间: {_currentArtwork.UpdateTime:yyyy-MM-dd HH:mm:ss}{vbCrLf}" &
                             $"备注: {_currentArtwork.Notes}{vbCrLf}"
        MessageBox.Show(info, "稿件信息", MessageBoxButtons.OK, MessageBoxIcon.Information)
    End Sub
    ''' <summary>
    ''' 显示帮助信息
    ''' </summary>
    Private Sub ShowHelp()
        Dim helpString As String = $"上一张        左箭头或上箭头,'W','A','P',PageUp,小于号{vbCrLf}" &
                                   $"下一张        右箭头或下箭头,'S','D','N',PageDown,大于号,空格或回车{vbCrLf}" &
                                   $"上一稿件        Ctrl+PageUp,Ctrl+上箭头,Ctrl+左箭头{vbCrLf}" &
                                   $"下一稿件        Ctrl+PageDown,Ctrl+下箭头,Ctrl+右箭头{vbCrLf}" &
                                   $"第一张        Home{vbCrLf}" &
                                   $"最后一张        End{vbCrLf}" &
                                   $"关闭        Esc{vbCrLf}" &
                                   $"显示信息        I{vbCrLf}" &
                                   $"切换全屏        F11{vbCrLf}" &
                                   $"显示帮助        F1"
        MessageBox.Show(helpString, "使用帮助说明", MessageBoxButtons.OK, MessageBoxIcon.Information)
    End Sub
    ''' <summary>
    ''' 置顶
    ''' </summary>
    Private Sub SetWindowOnTop()
        Dim hMenu = GetSystemMenu(Handle, False)
        If TopMost = False Then
            TopMost = True
            CheckMenuItem(hMenu, SC_ALWAYSONTOP, MF_CHECKED) '窗口置顶
        Else
            TopMost = False
            CheckMenuItem(hMenu, SC_ALWAYSONTOP, MF_UNCHECKED) '取消置顶
        End If
    End Sub
    ''' <summary>
    ''' 当库关闭时, 本窗口也将关闭
    ''' </summary>
    Private Sub OnLibraryClosed(sender As Object, e As EventArgs)
        If Me.InvokeRequired Then
            Me.Invoke(Sub() Me.Close())
        Else
            Me.Close()
        End If
    End Sub

    ''' <summary>
    ''' 异步加载图片
    ''' </summary>
    ''' <param name="filePath">图片路径</param>
    Private Async Sub LoadImageAsync(filePath As String)
        Try
            Dim hMenu = GetSystemMenu(Me.Handle, False)
            SyncLock _loadingLock
                If _isProcessing Then Return  '防止重复进入
                _isProcessing = True
            End SyncLock
            For Each i In {SC_PREVIMG, SC_NEXTIMG, SC_PREVART, SC_NEXTART}
                EnableMenuItem(hMenu, i, MF_BYCOMMAND Or MF_GRAYED)
            Next
            EnableMenuItem(hMenu, SC_COPY, MF_BYCOMMAND Or MF_GRAYED)
            '取消之前的加载任务
            _cancellationTokenSource?.Cancel()
            _cancellationTokenSource = New CancellationTokenSource()
            '显示加载提示
            PictureBoxMain.Image = Nothing
            Me.Text = "加载中... " & Path.GetFileName(filePath)
            Me.Cursor = Cursors.WaitCursor
            '异步加载图片
            Dim image = Await Task.Run(Function() LoadImageWithResize(filePath, 1920, 1080, _cancellationTokenSource.Token),
                                       _cancellationTokenSource.Token)
            '检查是否被取消
            If _cancellationTokenSource.Token.IsCancellationRequested Then
                image?.Dispose()
                Return
            End If
            '更新UI
            If image IsNot Nothing Then
                '释放旧图片
                If PictureBoxMain.Image IsNot Nothing Then
                    Dim oldImage = PictureBoxMain.Image
                    PictureBoxMain.Image = Nothing
                    oldImage.Dispose()
                End If
                PictureBoxMain.Image = image
                UpdateWindowTitle(filePath)
            End If
        Catch ex As OperationCanceledException
            '忽略取消事件
        Catch ex As Exception
            MessageBox.Show($"加载图片时出错: {ex.Message}", "Furry Art Studio",
                           MessageBoxButtons.OK, MessageBoxIcon.Error)
        Finally
            '无论如何都要释放加载状态
            SyncLock _loadingLock
                _isProcessing = False
                EnableMenuItem(GetSystemMenu(Me.Handle, False), 6, MF_BYCOMMAND Or MF_ENABLED)
            End SyncLock
            Me.Cursor = Cursors.Default
            UpdateMenuStates()
        End Try
    End Sub

    ''' <summary>
    ''' 在后台线程中加载并调整图片大小
    ''' </summary>
    ''' <param name="filePath">文件路径</param>
    ''' <param name="maxWidth">最大宽度</param>
    ''' <param name="maxHeight">最大高度</param>
    ''' <param name="cancellationToken">取消令牌</param>
    ''' <returns>新的图片</returns>
    Private Function LoadImageWithResize(filePath As String, maxWidth As Integer,
                                         maxHeight As Integer, cancellationToken As CancellationToken) As Image
        If Not File.Exists(filePath) Then Return Nothing
        Using fs As New FileStream(filePath, FileMode.Open,
                                             FileAccess.Read,
                                             FileShare.Read, 4096, True)
            cancellationToken.ThrowIfCancellationRequested()
            '使用Image.FromStream避免文件锁定
            Using original As Image = Image.FromStream(fs)
                cancellationToken.ThrowIfCancellationRequested()
                '如果图片小于最大尺寸，直接返回副本
                If original.Width <= maxWidth AndAlso original.Height <= maxHeight Then
                    Return New Bitmap(original)
                End If
                '计算缩放尺寸
                Dim ratio As Double = Math.Min(maxWidth / original.Width, maxHeight / original.Height)
                Dim newWidth As Integer = CInt(original.Width * ratio)
                Dim newHeight As Integer = CInt(original.Height * ratio)
                '创建缩放的图片
                Dim resized As New Bitmap(newWidth, newHeight)
                Using g As Graphics = Graphics.FromImage(resized)
                    g.InterpolationMode = Drawing2D.InterpolationMode.HighQualityBicubic
                    g.DrawImage(original, 0, 0, newWidth, newHeight)
                End Using
                cancellationToken.ThrowIfCancellationRequested()
                Return resized
            End Using
        End Using
    End Function

#End Region

#Region "图片导航"
    ''' <summary>
    ''' 导航到下一张/下一个稿件
    ''' </summary>
    Private Sub NavigateNext()
        Dim currentImageFiles As List(Of String) = GetCurrentArtworkImages()
        '如果当前稿件还有下一张图片
        If _currentFileIndex < currentImageFiles.Count - 1 Then
            _currentFileIndex += 1
            LoadImageAsync(currentImageFiles(_currentFileIndex))
            Return
        End If
        '当前稿件没有下一张图片, 尝试切换到下一个稿件
        If _allArtworks IsNot Nothing AndAlso _currentArtworkIndex < _allArtworks.Count - 1 Then
            '找到下一个有图片的稿件
            For i As Integer = _currentArtworkIndex + 1 To _allArtworks.Count - 1
                Dim nextArtwork As Artwork = _allArtworks(i)
                Dim nextImageFiles As List(Of String) = GetImageFiles(nextArtwork.FilePaths)
                If nextImageFiles.Count > 0 Then
                    '切换到下一个稿件的第一个图片
                    _currentArtworkIndex = i
                    _currentArtwork = nextArtwork
                    _currentFileIndex = 0
                    LoadImageAsync(nextImageFiles(0))
                    Return
                End If
            Next
        End If
        MessageBox.Show("已经是最后一张图片了", "Furry Art Studio", MessageBoxButtons.OK, MessageBoxIcon.Information)
    End Sub

    ''' <summary>
    ''' 导航到上一张/上一个稿件
    ''' </summary>
    Private Sub NavigatePrevious()
        Dim currentImageFiles As List(Of String) = GetCurrentArtworkImages()
        '如果当前稿件还有上一张图片
        If _currentFileIndex > 0 Then
            _currentFileIndex -= 1
            LoadImageAsync(currentImageFiles(_currentFileIndex))
            Return
        End If
        '当前稿件没有上一张图片, 尝试切换到上一个稿件
        If _allArtworks IsNot Nothing AndAlso _currentArtworkIndex > 0 Then
            '找到上一个有图片的稿件
            For i As Integer = _currentArtworkIndex - 1 To 0 Step -1
                Dim prevArtwork As Artwork = _allArtworks(i)
                Dim prevImageFiles As List(Of String) = GetImageFiles(prevArtwork.FilePaths)
                If prevImageFiles.Count > 0 Then
                    '切换到上一个稿件的最后一张图片
                    _currentArtworkIndex = i
                    _currentArtwork = prevArtwork
                    _currentFileIndex = prevImageFiles.Count - 1
                    LoadImageAsync(prevImageFiles(_currentFileIndex))
                    Return
                End If
            Next
        End If
        MessageBox.Show("已经是第一张图片了", "Furry Art Studio", MessageBoxButtons.OK, MessageBoxIcon.Information)
    End Sub

    ''' <summary>
    ''' 导航到第一张稿件
    ''' </summary>
    Private Sub NavigateToFirstArtwork()
        If _allArtworks Is Nothing Then Return
        For i As Integer = 0 To _allArtworks.Count - 1
            Dim imageFiles As List(Of String) = GetImageFiles(_allArtworks(i).FilePaths)
            If imageFiles.Count > 0 Then
                _currentArtworkIndex = i
                _currentArtwork = _allArtworks(i)
                _currentFileIndex = 0
                LoadImageAsync(imageFiles(0))
                Exit For
            End If
        Next
    End Sub

    ''' <summary>
    ''' 导航到最后一张稿件
    ''' </summary>
    Private Sub NavigateToLastArtwork()
        If _allArtworks Is Nothing Then Return
        For i As Integer = _allArtworks.Count - 1 To 0 Step -1
            Dim imageFiles As List(Of String) = GetImageFiles(_allArtworks(i).FilePaths)
            If imageFiles.Count > 0 Then
                _currentArtworkIndex = i
                _currentArtwork = _allArtworks(i)
                _currentFileIndex = 0
                LoadImageAsync(imageFiles(0))
                Exit For
            End If
        Next
    End Sub

    ''' <summary>
    ''' 导航到上一个稿件
    ''' </summary>
    Private Sub NavigatePreviousArtwork()
        If _allArtworks Is Nothing OrElse _allArtworks.Count = 0 Then Return
        '找到上一个有图片的稿件
        For i As Integer = _currentArtworkIndex - 1 To 0 Step -1
            Dim prevArtwork As Artwork = _allArtworks(i)
            Dim prevImageFiles As List(Of String) = GetImageFiles(prevArtwork.FilePaths)

            If prevImageFiles.Count > 0 Then
                _currentArtworkIndex = i
                _currentArtwork = prevArtwork
                _currentFileIndex = 0
                LoadImageAsync(prevImageFiles(0))
                Return
            End If
        Next
        MessageBox.Show("已经是第一个有图片的稿件了", "Furry Art Studio", MessageBoxButtons.OK, MessageBoxIcon.Information)
    End Sub
    ''' <summary>
    ''' 导航到下一个稿件
    ''' </summary>
    Private Sub NavigateNextArtwork()
        If _allArtworks Is Nothing OrElse _allArtworks.Count = 0 Then Return
        '找到下一个有图片的稿件
        For i As Integer = _currentArtworkIndex + 1 To _allArtworks.Count - 1
            Dim nextArtwork As Artwork = _allArtworks(i)
            Dim nextImageFiles As List(Of String) = GetImageFiles(nextArtwork.FilePaths)

            If nextImageFiles.Count > 0 Then
                _currentArtworkIndex = i
                _currentArtwork = nextArtwork
                _currentFileIndex = 0
                LoadImageAsync(nextImageFiles(0))
                Return
            End If
        Next
        MessageBox.Show("已经是最后一个有图片的稿件了", "Furry Art Studio", MessageBoxButtons.OK, MessageBoxIcon.Information)
    End Sub
#End Region

#Region "其他"
    '窗体键盘事件处理 - 使用窗体事件确保响应
    Private Sub ViewForm_KeyDown(sender As Object, e As KeyEventArgs) Handles MyBase.KeyDown
        If _isProcessing Then '当正在加载图片时, 取消处理按键响应
            e.Handled = True
            Return
        End If
        '处理组合键
        If e.Control Then
            Select Case e.KeyCode
                Case Keys.PageUp, Keys.Up, Keys.Left '上一个稿件
                    NavigatePreviousArtwork()
                    e.Handled = True
                    Return
                Case Keys.PageDown, Keys.Right, Keys.Down '下一个稿件
                    NavigateNextArtwork()
                    e.Handled = True
                    Return
                Case Keys.C
                    Clipboard.SetImage(PictureBoxMain.Image)
            End Select
        End If
        If e.Alt And e.KeyCode = Keys.T Then
            SetWindowOnTop()
            e.Handled = True
            e.SuppressKeyPress = True '防止发出声音
        End If
        '处理单键
        Select Case e.KeyCode
            Case Keys.Left, Keys.P, Keys.PageUp, Keys.Up, Keys.Oemcomma, Keys.A, Keys.W '上一张
                NavigatePrevious()
                e.Handled = True
            Case Keys.Right, Keys.N, Keys.PageDown, Keys.Down, Keys.OemPeriod, Keys.S, Keys.D,
                Keys.Space, Keys.Enter '下一张
                NavigateNext()
                e.Handled = True
            Case Keys.Home '第一张
                NavigateToFirstArtwork()
                e.Handled = True
            Case Keys.End '最后一张
                NavigateToLastArtwork()
                e.Handled = True
            Case Keys.Escape '退出
                Me.Close()
                e.Handled = True
            Case Keys.F11 '全屏切换
                ToggleFullScreen()
                e.Handled = True
            Case Keys.I '显示信息
                ShowArtworkInfo()
                e.Handled = True
            Case Keys.Insert '老板键
            Case Keys.F1
                ShowHelp()
        End Select
    End Sub
    '切换全屏模式
    Private Sub ToggleFullScreen()
        '全屏
    End Sub
    Private Sub PictureBoxMain_MouseDown(sender As Object, e As MouseEventArgs) Handles PictureBoxMain.MouseDown
        If e.Button = MouseButtons.Left Then
            '左键按下时模拟标题栏拖动
            ReleaseCapture()
            SendMessage(Me.Handle, WM_NCLBUTTONDOWN, HTCAPTION, 0)
        ElseIf e.Button = MouseButtons.Right Then
            '右键按下时显示系统菜单
            Dim hMenu As IntPtr = GetSystemMenu(Me.Handle, False) '获取系统菜单句柄
            If hMenu = IntPtr.Zero Then Return
            '将客户区坐标转换为屏幕坐标
            Dim screenPos As Point = Me.PointToScreen(New Point(e.X, e.Y))
            '获取要执行的菜单命令
            Dim cmd As Integer = TrackPopupMenu(hMenu, TPM_LEFTALIGN Or TPM_RETURNCMD,
                                            screenPos.X, screenPos.Y, 0, Me.Handle, IntPtr.Zero)
            '如果有命令, 发送给窗口
            If cmd <> 0 Then
                SendMessage(Me.Handle, WM_SYSCOMMAND, cmd, 0)
            End If
        End If
    End Sub
    Private Sub PictureBoxMain_MouseWheel(sender As Object, e As MouseEventArgs) Handles PictureBoxMain.MouseWheel
        If _isProcessing Then '当正在加载图片时, 取消处理按键响应
            Return
        End If
        If e.Delta > 0 Then
            NavigatePrevious()
        Else
            NavigateNext()
        End If
    End Sub
    ''' <summary>
    ''' 根据当前状态更新菜单项的启用/禁用
    ''' </summary>
    Private Sub UpdateMenuStates()
        Dim hMenu As IntPtr = GetSystemMenu(Me.Handle, False)
        If hMenu = IntPtr.Zero Then Return
        Dim currentImages As List(Of String) = GetCurrentArtworkImages()
        Dim isFirstImage As Boolean = (_currentArtworkIndex = 0 AndAlso _currentFileIndex = 0) '第一个稿件第一个文件
        Dim isLastImage As Boolean = (_currentArtworkIndex = _allArtworks.Count - 1 AndAlso
                                  _currentFileIndex = currentImages.Count - 1) '最后一个稿件最后一个文件
        If Not isFirstImage Then '判断是否为第一个文件
            EnableMenuItem(hMenu, SC_PREVIMG, MF_BYCOMMAND Or MF_ENABLED)
        Else
            EnableMenuItem(hMenu, SC_PREVIMG, MF_BYCOMMAND Or MF_GRAYED)
        End If
        If Not isLastImage Then '判断是否为最后一个文件
            EnableMenuItem(hMenu, SC_NEXTIMG, MF_BYCOMMAND Or MF_ENABLED)
        Else
            EnableMenuItem(hMenu, SC_NEXTIMG, MF_BYCOMMAND Or MF_GRAYED)
        End If
        If HasPreviousArtwork() Then '判断是否为第一个稿件
            EnableMenuItem(hMenu, SC_PREVART, MF_BYCOMMAND Or MF_ENABLED)
        Else
            EnableMenuItem(hMenu, SC_PREVART, MF_BYCOMMAND Or MF_GRAYED)
        End If
        If HasNextArtwork() Then '判断是否为最后一个稿件
            EnableMenuItem(hMenu, SC_NEXTART, MF_BYCOMMAND Or MF_ENABLED)
        Else
            EnableMenuItem(hMenu, SC_NEXTART, MF_BYCOMMAND Or MF_GRAYED)
        End If
    End Sub

    ''' <summary>
    ''' 检查是否存在上一个有图片的稿件
    ''' </summary>
    Private Function HasPreviousArtwork() As Boolean
        If _allArtworks Is Nothing OrElse _currentArtworkIndex <= 0 Then Return False

        For i As Integer = _currentArtworkIndex - 1 To 0 Step -1
            Dim imageFiles As List(Of String) = GetImageFiles(_allArtworks(i).FilePaths)
            If imageFiles.Count > 0 Then Return True
        Next

        Return False
    End Function

    ''' <summary>
    ''' 检查是否存在下一个有图片的稿件
    ''' </summary>
    Private Function HasNextArtwork() As Boolean
        If _allArtworks Is Nothing OrElse _currentArtworkIndex >= _allArtworks.Count - 1 Then Return False

        For i As Integer = _currentArtworkIndex + 1 To _allArtworks.Count - 1
            Dim imageFiles As List(Of String) = GetImageFiles(_allArtworks(i).FilePaths)
            If imageFiles.Count > 0 Then Return True
        Next

        Return False
    End Function
#End Region

End Class